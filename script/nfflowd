#!/usr/bin/perl

use strict;
use warnings FATAL => 'all';

use Net::Netfilter::NetFlow::Utils;
use Net::Netfilter::NetFlow::Process;

use IPC::Run 'run';
use Getopt::Long; # core

my $DEFAULT_CONFIG_FILE = '/etc/nfflowd.conf';
my $config_file = $DEFAULT_CONFIG_FILE;
my $help ='';

my $result = GetOptions(
    'h|help'     => \$help,
    'c|config=s' => \$config_file,
);
$help and &quit_with_help;

# bootstrap configuration:
# try requested, then default loc, then built-ins
my $local_config = load_config('nfflowd.conf');
my $config = eval{ load_config($config_file) };
if ($@ and $config_file ne $DEFAULT_CONFIG_FILE) {
    die "Failed to load requested config\n"
}

# mix in the built-ins
$config = merge_hashes($local_config, $config);

# make sure we have args for the flow_send app
if (ref $config->{flow_send}->{args} ne 'ARRAY'
    or scalar @{$config->{flow_send}->{args}} ne 3
) {
    die "You must supply three arguments for flow_send: source addr, dest addr, dest port\n";
}

# locate the required externals
my $conntrack = can_run($config->{conntrack}->{progname})
    or die "Failed to find a local copy of conntrack in the path\n";
my $flow_import = can_run($config->{flow_import}->{progname})
    or die "Failed to find a local copy of flow-import in the path\n";
my $flow_send = can_run($config->{flow_send}->{progname})
    or die "Failed to find a local copy of flow-send in the path\n";

# MAIN BODY
run [$conntrack, format_args($config->{conntrack})],
        \undef, init => conntrack_init($config),
    '|', ct2ft($config),
    '|', ptee($config),
    '|', [$flow_import, format_args($config->{flow_import})],
    '|', [$flow_send, format_args($config->{flow_send})];

__END__

=head1 AUTHOR

Oliver Gorwits C<< <oliver.gorwits@oucs.ox.ac.uk> >>

=head1 COPYRIGHT & LICENSE

Copyright (c) The University of Oxford 2009.

This library is free software; you can redistribute it and/or modify it under
the same terms as Perl itself.

=cut

